plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

ext {
    PUBLISH_GROUP_ID    = 'com.jnt.echarts'
    PUBLISH_ARTIFACT_ID = 'echarts-js'
    PUBLISH_PATH        = 'JTEXPRESS/echarts-js'
    PUBLISH_VERSION     = '1.0.1'
}

group   = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) from android.sourceSets.main.java.srcDirs
    else from sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                artifact androidSourcesJar

                pom {
                    name = 'Android Echart'
                    description = 'Android echart is Android library to using echart js (https://echarts.apache.org) for creating line chart, bar chart, pie chart, etc.'
                    url = 'https://github.com/'+PUBLISH_PATH
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'JTEXPRESS'
                            name = 'J&T Express'
                            email = 'jnt.itdeveloper@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/'+PUBLISH_PATH+'.git'
                        developerConnection = 'scm:git:ssh://github.com'+PUBLISH_PATH+'.git'
                        url = 'https://github.com/'+PUBLISH_PATH
                    }
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')

                        project.configurations.implementation.allDependencies.each {
                            if (it == null) return
                            if (it.group == null) return
                            if (it.name == null) return
                            if (it.name == 'unspecified') return
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username System.getenv("OSSRH_USERNAME")
                    password System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    testImplementation 'junit:junit:'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
